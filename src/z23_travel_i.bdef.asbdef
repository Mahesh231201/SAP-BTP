managed implementation in class zbp_23_travel_i unique;
strict ( 2 );   //  THESE ENSURE WE ARE FOLLOWING STRICT SYNTAX
with draft;

define behavior for Z23_TRAVEL_I alias Travel
persistent table z23_travel
with additional save
draft table z23_d_travel
lock master
total etag LocalLastChangedAt
authorization master ( global, instance )
etag master LocalLastChangedAt                            //  SYSTEM DETECT THE LATEST TIMESTAMP AND PREVENT OVERWRITE
{
  create;       //  USER CAN CREATE NEW TRAVELS
  update;       //  USER CAN UPDATE OR EDIT THE EXISTING TRAVELS
  delete;       //  USER CAN CANCEL / DELETE THE ENTIRE TRAVEL { INCLUDING ITS CHILD BOOKING AND BOOKINGSUPPL }
  field ( numbering : managed, readonly ) TravelUuid;    //  NONEDITABLE FIELD IN THE FIORI SCREEN
  field ( readonly ) TravelId, OverallStatus, TotalPrice;
  field ( features : instance ) BookingFee;
  association _Booking { create; with draft; }           //  USER CAN DO THE BOOKINGS TO THE CURRENT TRAVEL

  // Draft Related Buttons
  // We can Enable the Draft Related Functionalities

  draft action Edit;
  draft action Discard;
  draft action Resume;
  draft action Activate optimized;
  draft determine action Prepare
  { validation validatecustomer;
    validation validateagency;
    validation validatedates; }


  //  DETERMINATIONS

  determination setTravelId on save { create; update; }
  determination setOverallStatus on modify { create; update; }
  determination CalculateTotalPrice on modify { create; field BookingFee, CurrencyCode; }

  //  VALIDATIONS

  validation ValidateCustomer on save { create; field CustomerId; }
  validation ValidateAgency on save { create; field AgencyId; }
  validation ValidateDates on save { create; field BeginDate, EndDate; }

  //  ACTIONS

  action ( features : instance ) AcceptTravel result [1] $self;
  action ( features : instance ) RejectTravel result [1] $self;
  action DeductDiscount parameter Z23_A_DISCOUNT result [1] $self { default function GetDefaultsForDeductDiscount; }
  internal action recalctotalprice;

  determine action validatecustomerid { validation ValidateCustomer; }
  determine action validateagencyid { validation ValidateAgency; }
  determine action validatedatesval { validation ValidateDates; }

  //  SIDE EFFECTS

  side effects
  {

    field BookingFee affects field TotalPrice;
    determine action validateCustomerid executed on field CustomerId affects messages;
    determine action validateAgencyid executed on field AgencyId affects messages;
    determine action validatedatesval executed on field BeginDate, field EndDate affects messages;

  }

  //  Do mapping between CDS Entity Alias Names and Data base level Fields
  mapping for z23_travel //  database table
    {

      // CDS Entity Alias Name LHS = Database level field Name  RHS
      TravelUuid         = travel_uuid;
      TravelId           = travel_id;
      AgencyId           = agency_id;
      CustomerId         = customer_id;
      BeginDate          = begin_date;
      EndDate            = end_date;
      BookingFee         = booking_fee;
      TotalPrice         = total_price;
      CurrencyCode       = currency_code;
      Description        = description;
      OverallStatus      = overall_status;
      LocalCreatedBy     = local_created_by;
      LocalCreatedAt     = local_created_at;
      LocalLastChangedBy = local_last_changed_by;
      LocalLastChangedAt = local_last_changed_at;
      LastChangedAt      = last_changed_at;
    }

}

//              BOOKING

define behavior for Z23_BOOKING_I alias Booking
persistent table z23_booking
draft table z23_d_booking
lock dependent by _Travel
authorization dependent by _Travel
etag master LocalLastChangedAt
{
  update; // user can update or edit the booking once they inside the travel instance
  delete; // user can delete/cancel the Booking and its child Booking suppl
  field ( readonly ) travelUuid, BookingId, BookingDate;
  field ( numbering : managed, readonly ) BookingUuid;
  association _Travel { with draft; }
  association _BookingSupplement { create; with draft; }

  //  DETERMINATIONS
  determination SetBookingId on save { create; }
  determination SetBookingDate on save { create; }
  determination CalculateTotalPrice on modify { create; field FlightPrice, CurrencyCode; }

  //  SIDE EFFECTS

  side effects
  {
    field FlightPrice affects field _Travel.TotalPrice;
  }


  //  Do mapping between CDS Entity Alias Names and Data base level Fields
  mapping for z23_booking //  database table
    {

      // CDS Entity Alias Name LHS = Database level field Name  RHS
      BookingUuid        = booking_uuid;
      travelUuid         = parent_uuid;
      BookingId          = booking_id;
      BookingDate        = booking_date;
      CustomerId         = customer_id;
      CarrierId          = carrier_id;
      ConnectionId       = connection_id;
      FlightDate         = flight_date;
      FlightPrice        = flight_price;
      CurrencyCode       = currency_code;
      BookingStatus      = booking_status;
      LocalLastChangedAt = local_last_changed_at;
    }

}


//          BOOKING SUPPLEMENT

define behavior for Z23_BK_SUPPL_I alias BookingSupplement
persistent table z23_bk_suppl
draft table z23_d_bk_suppl
lock dependent by _Travel
authorization dependent by _Travel
etag master LocalLastChangedAt
{
  update;
  delete;
  field ( numbering : managed, readonly ) BooksupplUuid;
  field ( readonly ) travelUuid, BookingUuid, BookingSupplementId;
  association _Travel { with draft; }
  association _Booking { with draft; }

  //          DETERMINATIONS

  determination SetBookingSupplId on save { create; }
  determination CalculateTotalPrice on modify { create; field Price, CurrencyCode; }

  //          SIDE EFFECTS

  side effects
  {
    field Price affects field _Travel.TotalPrice;
  }

  //  Do mapping between CDS Entity Alias Names and Data base level Fields
  mapping for z23_bk_suppl //  database table
    {

      // CDS Entity Alias Name LHS = Database level field Name  RHS
      BooksupplUuid       = booksuppl_uuid;
      TravelUUID          = root_uuid;
      BookingUUID         = parent_uuid;
      BookingSupplementId = booking_supplement_id;
      SupplementId        = supplement_id;
      Price               = price;
      CurrencyCode        = currency_code;
      LocalLastChangedAt  = local_last_changed_at;
    }

}